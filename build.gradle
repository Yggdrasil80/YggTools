plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id 'io.freefair.aggregate-javadoc' version '6.6.1'
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'signing'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: "com.github.johnrengelman.shadow"

    version = '1.3.2'
    group = 'io.github.nullptr'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }

        withSourcesJar()
        withJavadocJar()
    }

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_1_8
        options.encoding = 'UTF-8'
    }

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:all')
        options.encoding = 'UTF-8'
    }

    shadowJar {
        minimize()
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
        testImplementation 'org.slf4j:log4j-over-slf4j:2.0.6'
    }
    
    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) { publication ->
                from components.java

                pom {
                    groupId = project.group
                    version = project.version
                    artifactId = "tools" + (!(project.name == "tools") ? "-" + project.name.toLowerCase() : "")
                    name = project.name
                    description = 'üõ†Ô∏è A complete, modular and flexible toolset for all your Java ‚òï projects'
                    url = 'https://github.com/nullptr-rs/nullptr-tools'

                    developers {
                        developer {
                            id = 'nullptr-rs'
                            name = 'nullptr-rs'
                            email = 'nullptr.rs@gmail.com'
                        }
                    }

                    licenses {
                        license {
                            name = 'GNU General Public License v3.0'
                            url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/nullptr-rs/nullptr-tools.git'
                        developerConnection = 'scm:git:ssh://github.com/nullptr-rs/nullptr-tools.git'
                        url = 'https://github.com/nullptr-rs/nullptr-tools/tree/master'
                    }
                }
            }
        }

        repositories {
            maven {
                url = "https://maven.pkg.github.com/nullptr-rs/nullptr-tools"

                credentials {
                    username System.getenv('GITHUB_USERNAME')
                    password System.getenv('GITHUB_TOKEN')
                }
            }
        }
    }

    signing {
        useInMemoryPgpKeys(System.getenv("GPG_PRIVATE_KEY"), System.getenv("GPG_PASSPHRASE"))
        sign publishing.publications.mavenJava
    }
}