plugins {
    id "com.github.johnrengelman.shadow" version "7.1.0"
    id 'io.freefair.aggregate-javadoc' version '6.4.1'
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'signing'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: "com.github.johnrengelman.shadow"

    version = '1.2.0'
    group = 'io.github.yggdrasil80'

    def development = !(System.getenv("RELEASE_TYPE") != null && System.getenv("RELEASE_TYPE").equalsIgnoreCase("releases"))
    def artifact = rootProject.name.toLowerCase() + (!(project.name == "Tools") ? "-" + project.name.toLowerCase() : "")

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }

        withSourcesJar()
        withJavadocJar()
    }

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_1_8
        options.encoding = 'UTF-8'
    }

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:all')
        options.encoding = 'UTF-8'
    }

    shadowJar {
        minimize()
    }

    repositories {
        mavenCentral()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) { publication ->
                from components.java

                pom {
                    groupId = project.group
                    version = project.version + (development ? "-SNAPSHOT" : "")
                    artifactId = artifact
                    name = project.name
                    description = 'YggTools is my tools library'
                    url = 'https://github.com/Yggdrasil80/YggTools'

                    developers {
                        developer {
                            id = 'yggdrasil80'
                            name = 'Yggdrasil80'
                            email = 'louisdechorivit@gmail.com'
                        }
                    }

                    licenses {
                        license {
                            name = 'GNU General Public License v3.0'
                            url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/Yggdrasil80/YggTools.git'
                        developerConnection = 'scm:git:ssh://github.com/Yggdrasil80/YggTools.git'
                        url = 'https://github.com/Yggdrasil80/YggTools/tree/' + System.getenv("RELEASE_TYPE")
                    }
                }
            }
        }

        repositories {
            maven {
                url = "https://repo.yggdrasil80.tech/" + System.getenv("RELEASE_TYPE") + "/"

                credentials {
                    username = System.getenv("MAVEN_USERNAME")
                    password = System.getenv("MAVEN_TOKEN")
                }
            }

            if (!development) {
                maven {
                    url = "https://maven.pkg.github.com/Yggdrasil80/YggTools"

                    credentials {
                        username System.getenv('GITHUB_USERNAME')
                        password System.getenv('GITHUB_TOKEN')
                    }
                }
            }
        }
    }

    signing {
        useInMemoryPgpKeys(System.getenv("GPG_PRIVATE_KEY"), System.getenv("GPG_PASSPHRASE"))
        sign publishing.publications.mavenJava
    }
}